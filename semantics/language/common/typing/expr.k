module C-TYPING-EXPR
     imports C-TYPING-SYNTAX
     imports C-TYPING-STRICTNESS-SYNTAX

     imports C-DYNAMIC-SYNTAX
     imports C-ENV-SYNTAX
     imports C-LITERAL-SYNTAX
     imports C-PROMOTION-SYNTAX
     imports C-SETTINGS-SYNTAX
     imports C-SYNTAX
     imports COMPAT-SYNTAX

     syntax Set ::= "assignmentLabels" [function]
     rule assignmentLabels =>
          SetItem(l('_:=_))
          SetItem(l('_*=_))
          SetItem(l('_/=_))
          SetItem(l('_%=_))
          SetItem(l('_+=_))
          SetItem(l('_-=_))
          SetItem(l('_<<=_))
          SetItem(l('_>>=_))
          SetItem(l('_&=_))
          SetItem(l('_^=_))
          SetItem(l('_|=_))

     syntax Set ::= "typeStrictLeftBinaryOperators" [function]
     rule typeStrictLeftBinaryOperators =>
          SetItem(l('_<<_))
          SetItem(l('_>>_))
          SetItem(l('_*=_))
          SetItem(l('_/=_))
          SetItem(l('_%=_))
          SetItem(l('_+=_))
          SetItem(l('_-=_))
          SetItem(l('_<<=_))
          SetItem(l('_>>=_))
          SetItem(l('_&=_))
          SetItem(l('_^=_))
          SetItem(l('_|=_))
          SetItem(l('_++))
          SetItem(l('_--))
          SetItem(l('--_))
          SetItem(l('++_))

     rule <k> typeof(K:K) => evalToType ~> typeof(MYHOLE) ...</k>
          (.Bag => <type> K:K </type>)
          [structural]
     rule <k> evalToType ~> typeof(MYHOLE) => T ...</k>
          (<type> T:Type </type> => .Bag)
          [structural]

     // these are sort of artifacts of other things
     rule <type> emptyValue => t(.Set, void) ...</type>
          [structural]
     rule <type> tv(_, T:Type) => T ...</type>
          [structural]
     //TODO(liyili):has already had the macro for the rule
     //rule <type> E1:K[E2:K] => *(E1 + E2) ...</type>
          //[structural]

     //! \begin{rulesGroup}
     /*@  \fromStandard{\source[n1570]{\para{6.5.7}{3}}}{
     The integer promotions are performed on each of the operands. The type of
     the result is that of the promoted left operand. \ldots
     }*/
     rule <type> T:Type << _ => promote(T) ...</type>
          [structural]
     rule <type> T:Type >> _ => promote(T) ...</type>
          [structural]
     //! \end{rulesGroup}

     /*@ %\begin{rulesGroup}
     \fromStandard{\source[n1570]{\para{6.5.3.4}{5}}}{
     The value of the result of both operators is implementation-defined, and
     its type (an unsigned integer type) is \cinline{size_t}, defined in
     \header{<stddef.h>} (and other headers).
     }*/
     rule <type> SizeofExpression(_) => t(SetItem(IntegerConstant), cfg:sizeut) ...</type>
          [structural]
     rule <type> SizeofType(_, _) => t(SetItem(IntegerConstant), cfg:sizeut) ...</type>
          [structural]

     rule <type> OffsetOf(_, _, _) => t(SetItem(IntegerConstant), cfg:sizeut) ...</type>

     rule <type>
               t(S:Set, pointerType(T:Type)) + _:IntegerType
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               _:IntegerType + t(S:Set, pointerType(T:Type))
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               t(S:Set, pointerType(T:Type)) - _:IntegerType
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               t(_, pointerType(_:Type)) - t(_, pointerType(_:Type))
               => t(.Set, cfg:ptrdiffut)
          ...</type>
          [structural]
     rule <type>
               t(S:Set, arrayType(T:Type, _)) + _:IntegerType
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               _:IntegerType + t(S:Set, arrayType(T:Type, _))
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               t(S:Set, arrayType(T:Type, _)) - _:IntegerType
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               t(S:Set, incompleteArrayType(T:Type)) + _:IntegerType
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               _:IntegerType + t(S:Set, incompleteArrayType(T:Type))
               => t(S, pointerType(T))
          ...</type>
          [structural]
     rule <type>
               t(S:Set, incompleteArrayType(T:Type)) - _:IntegerType
               => t(S, pointerType(T))
          ...</type>
          [structural]


     // c99 6.4.5
     rule <type>
               stringLiteral(S:String)
               => t(.Set, arrayType(t(.Set, char), lengthString(S) +Int 1))
          ...</type>
          [structural]
     rule <type>
               wstringLiteral(L:List)
               => t(.Set, arrayType(t(.Set, cfg:wcharut), size(L) +Int 1))
          ...</type>
          [structural]

     rule <type> X:CId => T ...</type>
          <types>... X |-> T:Type ...</types>
          [structural]

     /*@ \fromStandard{\source[n1570]{\para{6.5.17}{2}}}{
     The left operand of a comma operator is evaluated as a void expression;
     there is a sequence point between its evaluation and that of the right
     operand. Then the right operand is evaluated; the result has its type and
     value.
     }*/
     rule <type> Comma(list(_:List ListItem(K:K))) => K ...</type>
          [structural]

     /*@ \fromStandard{\source[n1570]{\para{6.5.2.2}{5}}}{
     If the expression that denotes the called function has type pointer to
     function returning an object type, the function call expression has the
     same type as that object type, and has the value determined as specified
     in 6.8.6.4. Otherwise, the function call has type \cinline{void}.
     }*/
     rule <type> Call(T:Type, _) => innerType(T) ...</type>
          [structural]

     /*@ \fromStandard{\source[n1570]{\para{6.5.2.3}{3}}}{
     A postfix expression followed by the \cinline{.} operator and an
     identifier designates a member of a structure or union object. The value
     is that of the named member, and is an lvalue if the first expression is
     an lvalue. If the first expression has qualified type, the result has the
     so-qualified version of the type of the designated member.
     }*/
     rule <type>
               t(_, structType(S:StructId)) . F:CId
               => T
          ...</type>
          <structs>... S |-> fieldInfo(_, _, (_ F |-> T:Type), _) ...</structs>
          [structural]
     rule <type>
               t(_, unionType(S:StructId)) . F:CId
               => T
          ...</type>
          <structs>... S |-> fieldInfo(_, _, (_ F |-> T:Type), _) ...</structs>
          [structural]

     /*@ \fromStandard{\source[n1570]{\para{6.5.3.2}{4}}}{
     The unary \cinline{*} operator denotes indirection. If the operand points
     to a function, the result is a function designator; if it points to an
     object, the result is an lvalue designating the object. If the operand has
     type ``pointer to type'', the result has type ``type''. \ldots
     }*/
     rule <type> * T:Type => innerType(T) ...</type>
          [structural]

     rule <type>
               CompoundLiteral(_, Spec:K, Decl:K, _)
               // sort of hackish, assumes cast will evaluate to type
               => Cast(Spec, Decl, emptyValue)
          ...</type>
          [structural]

     rule <type>
               L:KLabel(K:K,, _)
               => K:K
          ...</type>
          requires (l(L) in assignmentLabels)
          [structural]

     rule <type>
               L:KLabel(T:ArithmeticType,, T':ArithmeticType)
               => usualArithmeticConversion(T, T')
          ...</type>
          requires isArithBinConversionOp(L)
          [structural]

     rule <type>
               _:K ? T:ArithmeticType : T':ArithmeticType
               => usualArithmeticConversion(T, T')
          ...</type>
          [structural]
     rule <type> _:K ? T:Type : T:Type => T:Type ...</type>
          requires notBool isArrayType(T)
          [structural]

     rule <type>
               _:K ? (t(S:Set, arrayType(T:Type, _))
               => t(S:Set, pointerType(T))) : _
          ...</type>
          [structural]
     rule <type>
               _:K ? _:K : (t(S:Set, arrayType(T:Type, _))
               => t(S:Set, pointerType(T)))
          ...</type>
          [structural]

     // not safe, could be incompatible types
     rule <type>
               _ ? t(S:Set, pointerType(T:Type)) : t(_, pointerType(_:Type))
               => t(S, pointerType(T))
          ...</type>
          [structural]

     rule <type> L:KLabel(T:FloatType) => T ...</type>
          requires isArithUnaryOp(L)
          [structural]
     rule <type> L:KLabel(T:IntegerType) => promote(T) ...</type>
          requires isArithUnaryOp(L)
          [structural]

     rule <type> L:KLabel(T:FloatType,, _:KList) => T ...</type>
          requires (l(L) in typeStrictLeftBinaryOperators)
          [structural]
     rule <type> L:KLabel(T:IntegerType,, _:KList) => promote(T) ...</type>
          requires (l(L) in typeStrictLeftBinaryOperators)
          [structural]

     rule <type> L:KLabel(T:PointerType) => T ...</type>
          requires (
                    (L ==KLabel ('_++))
                    orBool (L:KLabel ==KLabel ('_--))
                    orBool (L:KLabel ==KLabel ('--_))
                    orBool (L:KLabel ==KLabel ('++_))
               )
          [structural]

     rule <type> !(_:K) => t(.Set, int) ...</type>
          [structural]

     rule <type> L:KLabel(_:K,, _:K) => t(.Set, int) ...</type>
          requires (L:KLabel ==KLabel '_==_)
               orBool (L:KLabel ==KLabel '_!=_)
               orBool (L:KLabel ==KLabel '_&&_)
               orBool (L:KLabel ==KLabel '_||_)
          [structural]

     rule <type> & T:Type => t(.Set, pointerType(T)) ...</type>
          [structural]

     /*TODO(liyi): may be an error
     rule <type> ExpressionLoc(K:K, L:CabsLoc) => K:K ...</type>
          <curr-program-loc> _ => filterCabsLoc(L) </curr-program-loc>
          [structural]
     */
endmodule

